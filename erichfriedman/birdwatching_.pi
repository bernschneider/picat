/* Janko Erich Friedman Birdwatching
https://erich-friedman.github.io/puzzle/birds/ */
import sat, util. 
line(Map, Z1,S1, Z2,S2) = 
    cond(Map.get({Z1,S1,Z2,S2}, 0) + Map.get({Z2,S2,Z1,S1}, 0) > 0, 1, 0).

vertex(Birds,Vs,Z,S) = Res =>
    CGrid = " rgybp",
    Res = cond(membchk({(Z,S),1}, Vs), ['*', CGrid[1+Birds[Z,S]]], "  ").

main(P) =>	 
    Grid = {{ 1, 1, 1, 1, 1, 1, 1, 1, 1},
            { 1, 0, 1, 0, 1, 0, 1, 0, 1},
            { 1, 1, 1, 1, 1, 1, 1, 1, 1},
            { 0, 1, 0, 1, 0, 1, 0, 1, 0},
            { 0, 1, 1, 1, 1, 1, 1, 1, 0},
            { 0, 1, 0, 1, 0, 1, 0, 1, 0},
            { 1, 1, 1, 1, 1, 1, 1, 1, 1},
            { 1, 0, 1, 0, 1, 0, 1, 0, 1},
            { 1, 1, 1, 1, 1, 1, 1, 1, 1}}, N = len(Grid),
    input(P, Cnt, Birds), 
    Vs = [ {(Z,S),_} : Z in 1..N, S in 1..N, Grid[Z,S] = 1], % all free squares can be visited
    Es = [ {(Z,S),(Zn,Sn),_} : {(Z,S),_} in Vs, {(Zn,Sn),_} in Vs, abs(Zn-Z) + abs(Sn-S) = 1],
    path_d(Vs, Es, (8,1), (2,9)),
    EMap = new_map(), 
    foreach({(Zv,Sv),(Z,S),B} in Es) EMap.put({Zv,Sv,Z,S}, B) end,
    foreach(Color in 1..5) 
        Cnt #= sum([B : {(Z,S),B} in Vs, Birds[Z,S] = Color])
    end,
    solve(vars(Vs) ++ vars(Es)),
    SHor = " -", SVer = " |",
    foreach (Z in 1..N-1)
        foreach(S in 1..N-1) printf("%s%c", vertex(Birds,Vs,Z,S), SHor[1 + line(EMap, Z,S, Z,S+1)] ) end, println(vertex(Birds,Vs,Z,N)),
        foreach(S in 1..N)   printf("%c  ",                        SVer[1 + line(EMap, Z,S, Z+1,S)] ) end, nl
    end,
    foreach(S in 1..N-1)     printf("%s%c", vertex(Birds,Vs,N,S), SHor[1 + line(EMap, N,S, N,S+1)] ) end, println(vertex(Birds,Vs,N,N)). 

    input(0, Cnt, Birds) =>
        Re=1, Gr=2, Ye=3, Bl=4, Pu=5,
        Cnt = 1, % number of birds of each color
        Birds = {{ 0, 0,Gr, 0, 0,Re, 0, 0, 0},
                 { 0, 0, 0, 0, 0, 0, 0, 0, 0},
                 { 0, 0, 0, 0, 0,Bl,Bl, 0, 0},
                 { 0, 0, 0, 0, 0,Gr, 0, 0, 0},
                 { 0, 0, 0, 0, 0, 0, 0,Ye, 0},
                 { 0, 0, 0, 0, 0,Pu, 0, 0, 0},
                 { 0, 0, 0,Re,Gr,Ye, 0,Bl, 0},
                 { 0, 0, 0, 0, 0, 0,Re, 0, 0},
                 { 0,Ye, 0, 0,Pu, 0, 0,Pu, 0}}.