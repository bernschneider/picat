/* Janko Erich Friedman Middlesum
https://erich-friedman.github.io/puzzle/honey/ */
import sat, util. 

main(No) ?=>
    input(No, M), NZ = len(M), NS = len(M[1]), M :: 0..9, 
    foreach(L in M.rows() ++ M.columns())
        N = len(L),
        count(0, L, N-3),
        Idx = new_list(3), Idx :: 1..N, increasing_strict(Idx), 
        V = new_list(3), V :: 1..9, 
        foreach(I in 1..3) element(Idx[I], L, V[I]) end,
        V[1] + V[3] #= V[2]
    end,
    solve(M),
    foreach(Z in 1..NZ, S in 1..NS) 
		if M[Z,S] > 0 then printf("%d|", M[Z,S]) else print(" |") end,
        if S = NS then nl end
    end, nl, fail.

input(0, M) =>
    M = {{_,_,_,2}, {_,9,_,_}, {7,_,_,_}, {_,_,3,_}}. /*
 |4|6|2|
5|9| |4|
7| |9|2|
2|5|3| | CPU time 0.02 seconds, korrekt */